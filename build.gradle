import org.gradle.plugins.lesscss.LessCss

buildscript {
    repositories {
        mavenLocal()
     }
    dependencies {
        classpath group: 'org.msgilligan', name: 'gradle-lesscss-plugin', version: '0.1-SNAPSHOT'
    }
}
apply plugin: "lesscss"

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    rhino
}

dependencies {
    rhino "rhino:js:1.7R2"
}

task downloadLessCssRhino(type: UrlDownload) {
    ext.filename = "less-rhino-1.1.3.js"
    ext.downloadDir = "build/downloads"
    url "https://raw.github.com/erwan/less.js/rhino/dist/$filename"
    destination file("$downloadDir/$filename")
}

lesscss {
    rhinoClasspath = configurations.rhino
    lessCssRhinoJs = files(downloadLessCssRhino)
}

task less(type: LessCss) {
    source fileTree("src/style") { include "*.less" }
    destination "$buildDir/less"
}

class UrlDownload extends DefaultTask {

    @Input
    String url
    
    @OutputFile
    File destination

    @TaskAction
    void doDownload() {
        getDestination().withOutputStream { out -> 
            new URL(getUrl()).withInputStream { out << it }
        }         
    }

}
